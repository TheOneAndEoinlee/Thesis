finish
/clear, start
/cwd, 'C:\Users\eoinl\ThesisCode\Thesis\APDL\Mechanism\21623serpentinetest1'
*ulib,macros,mac
N=38 !number of lines in connectivity file

*DIM, con, ARRAY, N, 6   ! Define array of size Nx5 (N total lines, 5 columns)

*VREAD, con(1,1), connectivity, txt, ,jik, 6, N, , 1 ! Read txt data into array, skipping the header line
(6F10.0)

! define parameters
pi = 3.1416
w = 5e-3        !width of beam
t1 = 0.4e-3     !thickness of thin flexure
t2 = 2.5e-3     !thickness of reinforced beam on bistable element
t3 = 2.5e-3     !thickness of reinforced beam on end-effector
BL1 = 4e-3      !length of thin flexure
BL2 = 24e-3     !length of reinforced beam on bistable element
h = 5e-3        !flexure spacing
h2 = 15e-3      !height of end-effector
sl = 6e-3       !support length
st = 2e-3       !support thickness
Lr1 = 35e-3     !length of reinforced beam on end-effector
L1 = 6e-3       !length of thin flexure on end-effector
Lr2 = 55e-3     !length of reinforced beam on end-effector
Lc2 = 45e-3     !position of coupling point to bifurcation elemenet to decision element
L2 = 6e-3       !length of bifurcation flexure

!parameters for serpentine spring elements
ts1 = 0.5e-3    !thickness of serpentine spring 1
ts2 = 0.8e-3    !thickness of serpentine spring 2
ls1 = 10.1e-3      !length of serpentine spring 1
ls2 = 14.6e-3      !length of serpentine spring 2
n1 = 18         !number of serpentine springs 1
n2 = 14         !number of serpentine springs 2
tc = 1e-3     !thickness of connecting segments
lc = 1e-3       !length of connecting segments

!parameter for thin spring
ts = 0.1e-3     !thickness of thin spring

beta1 = 0       !angle of thin flexure on bistable element
beta2 = 8       !angle of reinforced beam on bistable element
beta3 = 0       !angle of second thin flexure on bistable element
alpha = 22      !angle of reinforced beam on end-effector
alpha2 = 2       !angle of bifurcation element

bxoffset = 60e-3 !x offset of bifurcation element
uc = 2*w !unit cell spacing
Ncells = 1 ! number of unit cells

disp_in = 32e-3 !displacement of input node
!convert angles to radians
beta1 = beta1*pi/180
beta2 = beta2*pi/180
beta3 = beta3*pi/180
alpha = alpha*pi/180
alpha2 = alpha2*pi/180

!spring calculation
dist = (Lr1+2*L1)*sin(alpha)
springlength = 3*dist-h/2
springarea = 1e-6
springk = 120
springe = springk*springlength/springarea

!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!! Define Materials!!
!!!!!!!!!!!!!!!!!!!!!!!!!

/prep7
et, 1, beam188
keyopt, 1, 1, 1 ! Turn on large deflection
! et,2,link180,,1 !section assumed rigid with keyoption 2
et, 2, beam188
keyopt, 2, 1, 1 ! Turn on large deflection

!material for flexures
MP, EX, 1, 2.2E9  ! Young's modulus for material ID 1
MP, PRXY, 1, 0.3  ! Poisson's ratio for material ID 1
mptemp, 1, 0

!material for rigid bodies
MP, EX, 2, 200E12  ! Young's modulus for material ID 2
MP, PRXY, 2, 0.3  ! Poisson's ratio for material ID 2

MP, EX, 3, springe  ! Young's modulus for material ID 3


!reinforcement on bistable element
sectype, 1, beam, rect
secdata, t2, w
!thin flexure
sectype, 2, beam, rect
secdata, t1, w
!reinforcement on end-effector
sectype, 3, beam, rect
secdata, t3, w
!support beam section
sectype, 4, beam, rect
secdata, st, w
!rigid bodies beam section
sectype, 5, beam, rect
secdata, 1e-3, w

! !spring section
sectype, 6, beam, rect
secdata, ts1, w

sectype, 7, beam, rect
secdata, ts2, w

sectype, 8, beam, rect
secdata, tc, w

sectype, 9, beam, rect
secdata, ts, w


kpo = 0
z = 0

*do,k,1,Ncells
    /input,unitcellkp,inp
    
    *do,j,1,N
        !LOGIC TO CHECK THAT I DONT MESH SPRING ELEMENTS OUT OF RANGE
        *if,con(j,2)+kpo,LT,0, cycle
        *if,con(j,2)+kpo,gt,kpmax, cycle
        ! *if,con(j,2),eq,36,cycle !temp hold to prevent meshing of spring elements
        *use,meshline, kpo, con(j,1), con(j,2), con(j,3), con(j,4), con(j,5), con(j,6)
    *enddo


    *get,kpo,kp,0,num,maxd !get number of keypoints up to this point
    *if,k,eq,1,then
        *get,kpnum,kp,0,num,maxd !get number of keypoints in unit cell
    z = z + uc !increment z position of unit cell

*enddo





/eshape, 1
/view, 1, 1, 1, 1
eplot

/solu

antype, 0
nlgeom, on
eqslv, PCG
outres, all, all
autots, on
neqit, 100
deltim, 1e-3, 1e-5, 1e-2, on
lnsrch, auto
! arclen, on

!Set boundary conditions
!select nodes to be constrained
ksel,s,kp,,7,kpo,kpnum
ksel,a,kp,,10,kpo,kpnum
ksel,a,kp,,30,kpo,kpnum
ksel,a,kp,,33,kpo,kpnum
ksel,a,kp,,34,kpo,kpnum
ksel,a,kp,,35,kpo,kpnum
nslk,s
cm,fixed_nodes,node




cmsel,s,fixed_nodes
d,all,all
allsel

dk,topspring,all,all
dk,topspring,ux,disp_in
! ! solve



dk,middlespring,all,all
dk,middlespring,ux,disp_in

! dk,bottomspring,all,all
! dk,bottomspring,ux,disp_in


solve

*get, n_steps1, active, 0, solu, ncmss
!! DISPLAY DEFORMED STATE !!


/post1
/view, 1, 0, 0, 1
subset, last
plnsol,s,eqv


!!Render and save animation
antime,50,0.05
/anfile,save,resultsbifurcation,avi


/post26

ksel,s,kp,,topspring
nslk,s
*get, id_disp, node, 0, num, max
allsel

nsol, 2, id_disp, u, x, displacement
rforce, 3, id_disp, F, x, force 
xvar, 2
plvar, 3

*del, var_export
	*dim, var_export, table, n_steps1,3
	vget, var_export(1,0), 1
	vget, var_export(1,1), 2
	vget, var_export(1,2), 3
/output, 'resultspring1','txt'
	*vwrite, var_export(1,0), var_export(1,1), var_export(1,2)
	%g, %g, %g
/output, term


! solve

! time,2
! cmsel,s,connection_nodes
! f,all,fx,0

! cmsel,s,input_nodes
! d,all,uy,disp_in
! allsel
! solve