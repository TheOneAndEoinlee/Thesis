! Define the material properties

/PREP7
ALLSEL, ALL  ! Selects all entities
CMDELE,id_fixed_nodes
lclear, all  ! Clears all entities
LDELE, ALL  ! Deletes all lines
KDELE, ALL  ! Deletes all keypoints









!!!!!!!!!!!!!!!!!!!!!
!! define sections !!
!!!!!!!!!!!!!!!!!!!!!


!thin flexure
sectype, 2, beam, rect,
secdata, t1, w, 2, 2
secoffset, user, 0, 0
!reinforcement on bistable element
sectype, 1, beam, rect,
secdata, t2, w, 2, 2
secoffset, user, 0, 0

!reinforcement on end-effector
sectype, 3, beam, rect,
secdata, t3, w, 2, 2
secoffset, user, 0, 0

!support beam section
sectype, 4, beam, rect,
secdata, st, w, 2, 2
secoffset, user, 0, 0



!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! keypoints for geometry !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!bistable element


!bistable shuttle
k, 1, h/2, h/2, 0
k, 2, h/2, -h/2, 0
k, 3, -h/2, -h/2, 0
k, 4, -h/2, h/2, 0

!right side of bistable element
!top right flexure
k, 5, h/2+ BL1*cos(beta1), h/2 + BL1*sin(beta1), 0
k, 6, h/2+ BL1*cos(beta1)+ BL2*cos(beta2), h/2 + BL1*sin(beta1)+ BL2*sin(beta2), 0
k, 7, h/2+ BL1*cos(beta1)+ BL2*cos(beta2) + BL1*cos(beta3), h/2 + BL1*sin(beta1)+ BL2*sin(beta2)+ BL1*sin(beta3), 0 !fixed point

!bottom right flexure
k, 8, h/2+ BL1*cos(beta1), -h/2 + BL1*sin(beta1), 0
k, 9, h/2+ BL1*cos(beta1)+ BL2*cos(beta2), -h/2 + BL1*sin(beta1)+ BL2*sin(beta2), 0
k, 10, h/2+ BL1*cos(beta1)+ BL2*cos(beta2) + BL1*cos(beta3), -h/2 + BL1*sin(beta1)+ BL2*sin(beta2)+ BL1*sin(beta3), 0 !fixed point

!left side of bistable element
!top left flexure
k, 11, -h/2- BL1*cos(beta1), h/2 + BL1*sin(beta1), 0
k, 12, -h/2- BL1*cos(beta1)- BL2*cos(beta2), h/2 + BL1*sin(beta1)+ BL2*sin(beta2), 0
k, 13, -h/2- BL1*cos(beta1)- BL2*cos(beta2) - BL1*cos(beta3), h/2 + BL1*sin(beta1)+ BL2*sin(beta2)+ BL1*sin(beta3), 0

!bottom left flexure
k, 14, -h/2- BL1*cos(beta1), -h/2 + BL1*sin(beta1), 0
k, 15, -h/2- BL1*cos(beta1)- BL2*cos(beta2), -h/2 + BL1*sin(beta1)+ BL2*sin(beta2), 0
k, 16, -h/2- BL1*cos(beta1)- BL2*cos(beta2) - BL1*cos(beta3), -h/2 + BL1*sin(beta1)+ BL2*sin(beta2)+ BL1*sin(beta3), 0

!end-effector
!define local coordinate system at centre of end effector
local, 11, 0,-(Lr1+2*L1)*sin(alpha), -h*1.5-(Lr1+2*L1)*cos(alpha), 0 
k, 17, h*0.4, 0, 0 
k, 18, h*0.4,h,0
k, 19, -h*0.4, h, 0
k, 20, -h*0.4, -h, 0
k, 21, h*0.4, -h, 0 !input keypoint

!top right flexure
k, 22, h*0.4+ L1*sin(alpha), h + L1*cos(alpha), 0
k, 23, h*0.4+ L1*sin(alpha)+ Lr1*sin(alpha), h + L1*cos(alpha)+ Lr1*cos(alpha), 0
k, 24, h*0.4+ L1*sin(alpha)+ Lr1*sin(alpha)+ L1*sin(alpha), h + L1*cos(alpha)+ Lr1*cos(alpha)+ L1*cos(alpha), 0 

!top left flexure
k, 25, -h*0.4+ L1*sin(alpha), h + L1*cos(alpha), 0
k, 26, -h*0.4+ L1*sin(alpha)+ Lr1*sin(alpha), h + L1*cos(alpha)+ Lr1*cos(alpha), 0
k, 27, -h*0.4+ L1*sin(alpha)+ Lr1*sin(alpha)+ L1*sin(alpha), h + L1*cos(alpha)+ Lr1*cos(alpha)+ L1*cos(alpha), 0

!bottom right flexure
k, 28, h*0.4+ L1*sin(alpha), -h - L1*cos(alpha), 0
k, 29, h*0.4+ L1*sin(alpha)+ Lr1*sin(alpha), -h - L1*cos(alpha)- Lr1*cos(alpha), 0
k, 30, h*0.4+ L1*sin(alpha)+ Lr1*sin(alpha)+ L1*sin(alpha), -h - L1*cos(alpha)- Lr1*cos(alpha)- L1*cos(alpha), 0 !fixed point

!bottom left flexure
k, 31, -h*0.4+ L1*sin(alpha), -h - L1*cos(alpha), 0
k, 32, -h*0.4+ L1*sin(alpha)+ Lr1*sin(alpha), -h - L1*cos(alpha)- Lr1*cos(alpha), 0
k, 33, -h*0.4+ L1*sin(alpha)+ Lr1*sin(alpha)+ L1*sin(alpha), -h - L1*cos(alpha)- Lr1*cos(alpha)- L1*cos(alpha), 0 !fixed point

!support 
csys,0
k, 34, -h/2- BL1*cos(beta1)- BL2*cos(beta2) - BL1*cos(beta3), h/2 + BL1*sin(beta1)+ BL2*sin(beta2)+ BL1*sin(beta3)+sl, 0 !fixed point
k, 35, -h/2- BL1*cos(beta1)- BL2*cos(beta2) - BL1*cos(beta3), -h/2 + BL1*sin(beta1)+ BL2*sin(beta2)+ BL1*sin(beta3)-sl, 0   !fixed point

*get, disp_in, kp, 17, loc, x !get x coordinate of input node
*get, Line_ID1, line, 0, num, maxd ! start is # 0 

l,1, 2
l,2,24
l,24,27
l,27, 3
l,3, 4
l,4, 1


*get, Line_ID2, line, 0, num, maxd !! #lines till this point is 6

!bistable beam short flexures
l, 1, 5!top right flexure
l, 6, 7

l, 8, 2!bottom right flexure
l, 9, 10

l, 4, 11!top left flexure
l, 12, 13

l, 14, 3!bottom left flexure
l, 15, 16

*get, Line_ID3, line, 0, num, maxd ! #lines till this point is 14

!bistable beam reinforced beams
l, 5, 6!top right flexure
l, 8, 9!bottom right flexure
l, 11, 12!top left flexure
l, 14, 15!bottom left flexure
!define right side of bistable element lines

*get, Line_ID4, line, 0, num, maxd ! #lines till this point is 18

!define support
l,  13,16
l,  13,34
l,  16,35

*get, Line_ID5, line, 0, num, maxd ! #lines till this point is 21

!define end-effector lines
l,  17, 18 !input 
l,  18, 19
l,  19, 20   
l,  20, 21
l,  21, 17 !input


*get, Line_ID6, line, 0, num, maxd ! #lines till this point is 24

!end effector flexures
l,  18,22!top right flexure
l,  23,24

l,  19,25!top left flexure
l,  26,27

l,  21,28!bottom right flexure
l,  29,30

l,  20,31!bottom left flexure
l,  32,33

*get, Line_ID7, line, 0, num, maxd ! #lines till this point is 32

!end effector reinforced beams
l, 22,23
l, 25,26
l, 28,29
l, 31,32

*get, Line_ID8, line, 0, num, maxd ! #lines till this point is 36

mat, 1 !set element type to beam188


allsel
!mesh flexures
secnum, 2
lsel, s, line, , Line_ID2+1, Line_ID3
lesize, all, 0, , 10
lmesh, all
allsel

!mesh reinforced beams
secnum, 1
lsel, s, line, , Line_ID3+1, Line_ID4
lesize, all,0 , , ,5
lmesh, all
allsel

!mesh support
secnum, 4
lsel, s, line, , Line_ID4+1, Line_ID5
lesize, all,0 , , ,10
lmesh, all
allsel

!mesh end-effector flexures
secnum, 2
lsel, s, line, , Line_ID6+1, Line_ID7
lesize, all, 0, , ,20
lmesh, all
allsel

!mesh end-effector reinforced beams
secnum, 3
lsel, s, line, , Line_ID7+1, Line_ID8
lesize, all, 0, , ,5
lmesh, all
allsel


mat, 2
secnum, 2
lsel,s,line,,Line_ID1+1,Line_ID2
lesize,all,0, , ,3
lmesh,all
allsel

lsel,s,line,,Line_ID5+1,Line_ID6
lesize,all,0, , ,3
lmesh,all
allsel

!define boundary conditions

/eshape, 1
/view, 1, 1, 1, 1
eplot

!select nodes to be fixed 
ksel,s,,,7
ksel,a,,,10
ksel,a,,,34
ksel,a,,,35
ksel,a,,,30
ksel,a,,,33
nslk,s
cm,id_fixed_nodes,node
allsel

!select nodes to be input displacement
ksel,s,,,17
nslk,s
*get, id_disp, node, , num, min

/eof 