finish
/clear, start
/cwd, 'C:\Users\eoinl\ThesisCode\Thesis\APDL\Mechanism'
*ulib,macros,mac
N=57 !number of lines in connectivity file

*DIM, con, ARRAY, N, 6   ! Define array of size Nx5 (N total lines, 5 columns)

*VREAD, con(1,1), connectivity, txt, ,jik, 6, N, , 1 ! Read txt data into array, skipping the header line
(6F10.0)

! define parameters
pi = 3.1416
w = 5e-3        !width of beam
t1 = 0.4e-3     !thickness of thin flexure
t2 = 2.5e-3     !thickness of reinforced beam on bistable element
t3 = 2.5e-3     !thickness of reinforced beam on end-effector
BL1 = 4e-3      !length of thin flexure
BL2 = 24e-3     !length of reinforced beam on bistable element
h = 5e-3        !flexure spacing
sl = 6e-3       !support length
st = 2e-3       !support thickness
Lr1 = 35e-3     !length of reinforced beam on end-effector
L1 = 6e-3       !length of thin flexure on end-effector
Lr2 = 55e-3     !length of reinforced beam on end-effector
Lc2 = 45e-3     !position of coupling point to bifurcation elemenet to decision element
L2 = 6e-3       !length of bifurcation flexure

beta1 = 0       !angle of thin flexure on bistable element
beta2 = 8       !angle of reinforced beam on bistable element
beta3 = 0       !angle of second thin flexure on bistable element
alpha = 22      !angle of reinforced beam on end-effector
alpha2 = 2       !angle of bifurcation element

bxoffset = 60e-3 !x offset of bifurcation element
uc = 2*w !unit cell spacing
Ncells = 4 ! number of unit cells

disp_in = 17e-3 !displacement of input node
!convert angles to radians
beta1 = beta1*pi/180
beta2 = beta2*pi/180
beta3 = beta3*pi/180
alpha = alpha*pi/180
alpha2 = alpha2*pi/180

!spring calculation
dist = (Lr1+2*L1)*sin(alpha)
springlength = 3*dist-h/2
springarea = 1e-6
springk = 120
springe = springk*springlength/springarea

!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!! Define Materials!!
!!!!!!!!!!!!!!!!!!!!!!!!!

/prep7
et, 1, beam188
keyopt, 1, 1, 1 ! Turn on large deflection
! et,2,link180,,1 !section assumed rigid with keyoption 2
et, 2, beam188
keyopt, 2, 1, 1 ! Turn on large deflection

!material for flexures
MP, EX, 1, 2.2E9  ! Young's modulus for material ID 1
MP, PRXY, 1, 0.3  ! Poisson's ratio for material ID 1
mptemp, 1, 0

!material for rigid bodies
MP, EX, 2, 200E12  ! Young's modulus for material ID 2
MP, PRXY, 2, 0.3  ! Poisson's ratio for material ID 2

MP, EX, 3, springe  ! Young's modulus for material ID 3


!reinforcement on bistable element
sectype, 1, beam, rect
secdata, t2, w
!thin flexure
sectype, 2, beam, rect
secdata, t1, w
!reinforcement on end-effector
sectype, 3, beam, rect
secdata, t3, w
!support beam section
sectype, 4, beam, rect
secdata, st, w
!rigid bodies beam section
sectype, 5, beam, rect
secdata, 1e-3, w

! !spring section
! sectype, 6, link
! secdata, springarea
! seccontrol, ,1

! !spring section
sectype, 6, beam, rect
secdata, 1e-3, 1e-3


kpoffset = 0

!do first iteration to get number of kps
*use, unitcellkp,0,0
*get,kpnum,kp,0,num,maxd
kpoffset = kpoffset + kpnum

!GENERATE REST OF UNIT CELLS
*do,j,1,Ncells-1
*use, unitcellkp,kpoffset,j*uc
kpoffset = kpoffset + kpnum
*enddo
*get,kpmax,kp,0,num,maxd


!mesh all keypoints 
kpoffset =0
*do,j,1,Ncells
*do,i,1,N
    !LOGIC TO CHECK THAT I DONT MESH SPRING ELEMENTS OUT OF RANGE
    *if,con(i,2)+kpoffset,LT,0, cycle
    *if,con(i,2)+kpoffset,gt,kpmax, cycle
    ! *if,con(i,2),eq,36,cycle !temp hold to prevent meshing of spring elements
    *use,meshline, kpoffset, con(i,1), con(i,2), con(i,3), con(i,4), con(i,5), con(i,6)
*enddo
kpoffset = kpoffset + kpnum
*enddo



/eshape, 1
/view, 1, 1, 1, 1
eplot

!!!!!!!!!!!!!!!!!!!!!!!!!   
!!!!!! SOLUTION !!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!
/solu

antype, 0
nlgeom, on
eqslv, sparse
outres, all, all
autots, on
neqit, 50
deltim, 1e-3, 1e-6, 1e-2, on
! arclen, on

!Set boundary conditions

!select nodes to be constrained
ksel,s,kp,,7,kpmax,kpnum
ksel,a,kp,,10,kpmax,kpnum
ksel,a,kp,,30,kpmax,kpnum
ksel,a,kp,,33,kpmax,kpnum
ksel,a,kp,,34,kpmax,kpnum
ksel,a,kp,,35,kpmax,kpnum
ksel,a,kp,,43,kpmax,kpnum
ksel,a,kp,,46,kpmax,kpnum
nslk,s
cm,fixed_nodes,node
allsel

!select nodes to be input
ksel,s,kp,,49,kpmax,kpnum
ksel,a,kp,,52,kpmax,kpnum
nslk,s
cm,input_nodes,node
allsel



cmsel,s,fixed_nodes
d,all,all
allsel

!set input displacement
cmsel,s,input_nodes
d,all,all
d,all,uy,disp_in
allsel

solve


!todo: isolate nodes for boundary conditions
!todo: create link167 elements between unit cells
!todo: calculate minimum flexure thickness for bifurcation element'
!todo: simulate with link167 elements without bifurcation element
!todo: simulate with link167 elements with bifurcation element from singular initial state




