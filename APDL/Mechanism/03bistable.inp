finish
/clear, start
/cwd, 'C:\Users\eoinl\ThesisCode\Thesis\APDL\Mechanism'
/filname, bistable_mechanism
nsteps = 1 !number of steps in sweep


! define parameters
w = 5e-3    !width of beam
t1 = 0.4e-3   !thickness of thin flexure
t2 = 2.5e-3   !thickness of reinforced beam on bistable element
t3 = 2.5e-3   !thickness of reinforced beam on end-effector
BL1 = 4e-3     !length of thin flexure
BL2 = 24e-3    !length of reinforced beam on bistable element
h = 5e-3     !flexure spacing
sl = 6e-3     !support length
st = 2e-3     !support thickness
beta1 = 0              !angle of thin flexure on bistable element
beta2 = 8              !angle of reinforced beam on bistable element
beta3 = 0              !angle of second thin flexure on bistable element
youngs = 2.2E9

! ! chen parameters
! w = 6e-3    !width of beam
! t1 = 0.8e-3   !thickness of thin flexure
! t2 = 3e-3   !thickness of reinforced beam on bistable element
! BL1 = 8e-3     !length of thin flexure
! BL2 = 20e-3    !length of reinforced beam on bistable element
! h = 5e-3     !flexure spacing
! sl = 1e-3     !support length
! st = 2e-3     !support thickness
! beta1 = 0              !angle of thin flexure on bistable element
! beta2 = 12              !angle of reinforced beam on bistable element
! beta3 = 0              !angle of second thin flexure on bistable element
! youngs = 1.4E9

!define vectors for sweeps
!Bl1
*dim, BL1_vec, array, nsteps
*vfill, BL1_vec,ramp,BL1, 0 !constant as no increment
!beta13
*dim, beta13_vec, array, nsteps
*vfill, beta13_vec,ramp,beta1, 0
!beta2
*dim, beta2_vec, array, nsteps
*vfill, beta2_vec,ramp,beta2,0 
!BL2
*dim, BL2_vec, array, nsteps
*vfill, BL2_vec,ramp,BL2,0
!sl
*dim, sl_vec, array, nsteps
*vfill, sl_vec,ramp,sl,0





!copt 







/prep7
et, 1, beam188
keyopt, 1, 1, 1 ! Turn on large deflection
MP, EX, 1, youngs  ! Young's modulus for material ID 1
MP, PRXY, 1, 0.3  ! Poisson's ratio for material ID 1
mptemp, 1, 0


MP, EX, 2, 200E12  ! Young's modulus for material ID 2
MP, PRXY, 2, 0.3  ! Poisson's ratio for material ID 2


! Loop over BL1 values and run analysis
*do, i, 1, nsteps
! Assign BL1 value
BL1 = BL1_vec(i)
! Assign beta13 value
beta1 = beta13_vec(i)*3.1416/180
beta3 = beta13_vec(i)*3.1416/180
! Assign beta2 value
beta2 = beta2_vec(i)*3.1416/180
! Assign BL2 value
BL2 = BL2_vec(i)
! Assign sl value
sl = sl_vec(i)


!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!geometry setup  !!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!
/input, bistable_setup, inp


allsel

!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!! loads !!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!


/solu

antype, 0
nlgeom, on
eqslv, sparse
outres, all, all
autots, on
neqit, 30
deltim, 1e-3, 1e-5, 1e-2, on

!define bcs
cmsel,s,id_fixed_nodes
d,all, all
allsel

time,1
d,id_disp,uy,2.1*((BL1)*sin(beta1)+BL2*sin(beta2)+BL1*sin(beta3))

allsel

solve

time,2 
d,id_disp,uy,0
solve
*get, n_steps1, active, 0, solu, ncmss
!! DISPLAY DEFORMED STATE !!


/post1
/view, 1, 0, 0, 1
subset, last
plnsol,s,eqv


! !!Render and save animation
antime,20,0.05
/anfile,save,results%i%,avi


!plotting final force displacement curve
/post26
nsol, 2, id_disp, u, y, displacement
rforce, 3, id_disp, F, y, force 
xvar, 2
plvar, 3


!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!  save plot .txt file !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!


/input, scratch, gui


*enddo